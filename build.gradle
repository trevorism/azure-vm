buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url uri("http://trevorism-build.eastus.cloudapp.azure.com/nexus/repository/maven-releases")
        }
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.1.0'
        classpath 'com.trevorism:gradle-acceptance-plugin:1.3.4'
        classpath 'com.trevorism:gradle-gae-deploy-plugin:1.1.1'
        classpath 'com.trevorism:gradle-release-plugin:0.3.0'
    }
}

plugins {
    id "groovy"
    id "war"
    id "jacoco"
}

apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: "com.trevorism.gradle.acceptance"
apply plugin: "com.trevorism.gradle.gae.deploy"
apply plugin: "com.trevorism.gradle.release"

repositories {
    mavenCentral()
    maven {
        url uri("http://trevorism-build.eastus.cloudapp.azure.com/nexus/repository/maven-releases")
    }
}

def appengineVersion = "1.9.76"
def jerseyVersion = "2.29.1"

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.8'

    compile "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"
    compile "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
    compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.10.0'
    compile 'com.trevorism:secure-utils:1.0.2'

    compile 'com.trevorism:eventhub-client:1.4.2'
    compile 'io.swagger:swagger-jersey2-jaxrs:1.5.23'
    compile 'com.microsoft.azure:azure:1.27.0'
    compile 'org.slf4j:slf4j-nop:1.7.28'

    testCompile 'junit:junit:4.12'
    testCompile "com.google.appengine:appengine-testing:$appengineVersion"
    testCompile "com.google.appengine:appengine-api-stubs:$appengineVersion"
    testCompile "com.google.appengine:appengine-api-labs:$appengineVersion"
    testCompile "com.google.appengine:appengine-tools-sdk:$appengineVersion"
}

processResources{
    doLast{
        copy{
            from "${System.getProperty("user.home")}/${project.name}/secrets.properties"
            into "build/resources/main"
        }
    }
}

acceptance {
    stepDefinitionRoots = ['src/acceptance/groovy']
    finalizedBy("sendAcceptanceTestEvents")
}

appengine {
    deploy {
        projectId = "trevorism-gcloud"
        version = "0-2-0"
        stopPreviousVersion = true
        promote = true
    }
}

check {
    dependsOn('jacocoTestReport')
}

task deliver {
    dependsOn("appengineDeploy", "acceptance")
    acceptance.mustRunAfter("appengineDeploy")
    appengineDeploy.mustRunAfter("check")
}